apply plugin: 'com.android.library'
apply plugin: 'com.github.alexfu.androidautoversion'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30

        versionCode androidAutoVersion.versionCode
        versionName androidAutoVersion.versionName + "." + (System.getenv("GITHUB_RUN_ID") ?: "0")

        setProperty("archivesBaseName", "networktest")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'fr.bmartel:http-endec:1.04'
    implementation 'commons-net:commons-net:3.6'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'net.jodah:concurrentunit:0.4.2'
    testImplementation 'com.googlecode.json-simple:json-simple:1.1.1'
    testImplementation 'com.google.code.gson:gson:2.8.6'
}

def sweeprProperties = new Properties()
def sweeprPropertiesFile = rootProject.file("sweepr.properties")

if (sweeprPropertiesFile.exists()) {
    sweeprProperties.load(new FileInputStream(sweeprPropertiesFile))
} else {
    sweeprProperties.setProperty('github.username', System.getenv("GPR_USER") ?: "undefined-user")
    sweeprProperties.setProperty('github.token', System.getenv("GPR_API_KEY") ?: "undefined-token")
}

publishing {
    publications {
        release(MavenPublication) {
            groupId 'com.sweepr'
            artifactId 'networktest'
            version androidAutoVersion.versionName

            artifact("$buildDir/outputs/aar/networktest-release.aar")

            pom {
                name = 'NetworkTest'
                description = 'Discovery UPnP devices via SSDP protocol'
                url = 'https://github.com/rosebud-iot/speed-test-lib.git'

                licenses {
                    license {
                        name = 'MIT'
                    }
                }
            }

            // include dependencies
            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.getByName("releaseCompileClasspath").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.moduleGroup)
                    dependency.appendNode('artifactId', it.moduleName)
                    dependency.appendNode('version', it.moduleVersion)
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/rosebud-iot/mobile-framework-android")
            credentials {
                username = sweeprProperties["github.username"]
                password = sweeprProperties["github.token"]
            }
        }
    }
}
