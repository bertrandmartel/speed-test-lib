apply plugin: 'com.android.library'
apply plugin: 'com.github.alexfu.androidautoversion'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.0"

    namespace "com.sweepr.networking"
    testNamespace "com.sweepr.networking"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 33

        versionCode androidAutoVersion.versionCode
        versionName androidAutoVersion.versionName + "." + (System.getenv("GITHUB_RUN_ID") ?: "0")

        multiDexEnabled true
        setProperty("archivesBaseName", "networking")

        testApplicationId "com.sweepr.framework.androidTest"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant("release") {
            withJavadocJar()
        }
        /*
        multipleVariants {
            withJavadocJar()
            allVariants()
        }
         */
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'fr.bmartel:http-endec:1.04'
    implementation 'commons-net:commons-net:3.6'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'net.jodah:concurrentunit:0.4.2'
    testImplementation 'com.googlecode.json-simple:json-simple:1.1.1'
    testImplementation 'com.google.code.gson:gson:2.8.9'

    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:all" // << "-Werror"
    }
}

def sweeprProperties = new Properties()
def sweeprPropertiesFile = rootProject.file("sweepr.properties")

if (sweeprPropertiesFile.exists()) {
    sweeprProperties.load(new FileInputStream(sweeprPropertiesFile))
} else {
    sweeprProperties.setProperty('github.username', System.getenv("GPR_USER") ?: "undefined-user")
    sweeprProperties.setProperty('github.token', System.getenv("GPR_API_KEY") ?: "undefined-token")
}

// Ensure that the publish task depends on assembly
tasks.all { task ->
    if (task instanceof AbstractPublishToMaven) {
        task.dependsOn assemble
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubInternalPackages"
            url = uri("https://maven.pkg.github.com/rosebud-iot/mobile-framework-android")
            credentials {
                username = sweeprProperties["github.username"]
                password = sweeprProperties["github.token"]
            }
        }

        maven {
            name = "GitHubPublicPackages"
            url = uri("https://maven.pkg.github.com/sweepr-sdk/android")
            credentials {
                username = sweeprProperties["github.username"]
                password = sweeprProperties["github.token"]
            }
        }
    }

    publications {
        release(MavenPublication) {
            groupId 'com.sweepr'
            artifactId 'networking'
            version androidAutoVersion.versionName

            artifact("$buildDir/outputs/aar/networking-release.aar")

            pom {
                name = 'NetworkTest'
                description = 'Discovery UPnP devices via SSDP protocol'
                url = 'https://github.com/rosebud-iot/speed-test-lib.git'

                licenses {
                    license {
                        name = 'MIT'
                    }
                }
            }

            // include dependencies
            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.getByName("releaseCompileClasspath").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.moduleGroup)
                    dependency.appendNode('artifactId', it.moduleName)
                    dependency.appendNode('version', it.moduleVersion)
                }
            }
        }
    }
}
